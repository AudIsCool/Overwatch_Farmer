/*
*   Name:        EventLogs.ostw
*   Author:      leguminote#5816
*   Description: This file holds the code to log Player Event info
*/
import "./HelperFunctions/StringUtils.ostw";

import "./PlayerStateInitRules.ostw";

playervar define TrackedHero = HeroOf(EventPlayer());   //Used for hero swaps
playervar define Dupping = false;                       //Used for printing correct output only on dup end

/*
*  @name        TrackRez
*  @description Log control rezzurected player event
*/
rule: "TrackRez" Event.OngoingPlayer
    if(IsAlive(EventPlayer()))
    if(HasSpawned(EventPlayer()))
    {
        if(IsGameInProgress())
        if(IsHeroBeingPlayed(Hero.Mercy, TeamOf(EventPlayer())))
        if(HasStatus(EventPlayer(), Status.PhasedOut))
        {
            Log(<
                    "[RESURRECT] Team [<0>] Player [<1>] Rezzed [<2>] playing [<3>]",
                    TeamOf(EventPlayer()),
                    AllPlayers(TeamOf(EventPlayer()))
                        [IndexOfArrayValue(
                            MappedArray(AllPlayers(TeamOf(EventPlayer())), HeroOf(ArrayElement())), 
                            Hero.Mercy)
                        ],
                    EventPlayer(),
                    HeroOf(EventPlayer())
                >);
        }
    }

/*
*  @name        TrackNormalUltReady
*  @description Log normal ult ready
*/
rule: "TrackNormalUltReady" Event.OngoingPlayer
    if(IsGameInProgress())
    if(UltimateChargePercent(EventPlayer()) == 100)
    if(HeroOf(EventPlayer()) != Hero.Echo)
    if(HeroOf(EventPlayer()) != Hero.Dva)
    {
        Log(<
            "[ULT_READY] [NORMAL] [<0>] on [<1>] [<2>] has 100% ult charge",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer())
        >);
    }

/*
*  @name        TrackEchoUltReady
*  @description Log Echo ult ready
*/
rule: "TrackEchoUltReady" Event.OngoingPlayer Team.All Player.Echo
    if(IsGameInProgress())
    if(UltimateChargePercent() == 100)
    {
        if(Not(PlayerState.ActualHeroBeingDuplicated(EventPlayer())))
        {
            Log(<
                "[ULT_READY] [NORMAL] [<0>] on [<1>] [<2>] has 100% ult charge",
                EventPlayer(),
                HeroOf(EventPlayer()),
                TeamOf(EventPlayer())
            >);
        } else {
            //Possibly change to handle duplicated d.va bomb vs duplicated d.va remech
            Log(<
                "[ULT_READY] [ECHO_DUPLICATE] [<0>] on [<1>] [<2>] (duplicated [<3>] hero) has 100% ult charge",
                EventPlayer(),
                HeroOf(EventPlayer()),
                TeamOf(EventPlayer()),
                PlayerState.ActualHeroBeingDuplicated(EventPlayer())
            >);
        }
    }

/*
*  @name        TrackD.vaUltReady
*  @description Log D.va ult ready
*/
rule: "TrackD.vaUltReady" Event.OngoingPlayer Team.All Player.Dva 
    if(IsGameInProgress())
    if(UltimateChargePercent(EventPlayer()) == 100)
    {
        if(Not(PlayerState.ActualAlternateForm(EventPlayer())))
        {
            Log(<
                "[ULT_READY] [NORMAL] [<0>] on [<1>] [<2>] has 100% ult charge",
                EventPlayer(),
                HeroOf(EventPlayer()),
                TeamOf(EventPlayer())
            >);
        } else {
            Log(<
                "[ULT_READY] [BABY_DVA] [<0>] on [<1>] [<2>] has 100% ult charge",
                EventPlayer(),
                HeroOf(EventPlayer()),
                TeamOf(EventPlayer()),
                PlayerState.ActualHeroBeingDuplicated(EventPlayer())
            >);
        }
    }

/*
*  @name        TrackNormalUltUsed
*  @description Log ults used (not d.va or echo)
*/
rule: "TrackNormalUltUsed" Event.OngoingPlayer
    if(IsGameInProgress())
    if(IsUsingUltimate(EventPlayer()))
    if(HeroOf(EventPlayer()) != Hero.Echo)
    if(HeroOf(EventPlayer()) != Hero.Dva)
    {
        Log(<
            "[ULT_USED] [<0>] [<1>], [<2>] used Ultimate",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer())
        >);
    }

/*
*  @name        TrackEchoUltDuringDup
*  @description Log ults used while duplicating a hero
*/
rule: "TrackEchoUltDuringDup" Event.OngoingPlayer Team.All Player.Echo
    if(IsGameInProgress())
    if(PlayerState.ActualHeroBeingDuplicated(EventPlayer()))
    if(IsUsingUltimate(EventPlayer()))
    {
        Log(<
            "[ECHO_ULTED_DURING_DUPLICATION] [<0>] [<1>], [<2>] used Ultimate (as [<3>])",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer()),
            PlayerState.ActualHeroBeingDuplicated(EventPlayer())
        >);
    }

/*
*  @name        TrackEchoDupUltUsed
*  @description Log echo duplications
*/
rule: "TrackEchoDupUltUsed" Event.OngoingPlayer Team.All Player.Echo
    if(IsGameInProgress())
    if(IsAlive())
    if(CurrentPlayerState.DuplicateStartEvent())
    {
        Log(<
            "[ECHO_DUPLICATE] [<0>] [<1>], [<2>] used ultimate duplicating [<3>]",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer()),
            PlayerState.ActualHeroBeingDuplicated(EventPlayer())
        >);
        Dupping = true;
    }

//TODO Echo Duplicate End  // MAYBE CHANGE TO SHOW WHO ENDED IT??
rule: "TrackDuplicateEnd" Event.OngoingPlayer Team.All Player.Echo
    if(IsGameInProgress())
    if(IsAlive())
    if(CurrentPlayerState.DuplicateEndedEvent())
    if(Dupping)
    {
        Log(<
            "[ECHO_DUPLICATE_END] [<0>] [<1>], [<2>] [<3>] duplicate ended",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer()),
            PlayerState.ActualHeroBeingDuplicated(EventPlayer()),
            PlayerState.ActualAlternateForm(EventPlayer())
        >);
        Dupping = false;
    }

rule: "TrackDuplicateDeath" Event.OnDamageTaken Team.All Player.Echo 
    if(IsGameInProgress())
    if(Health(EventPlayer()) < 0.5)
    if(PlayerState.ActualHeroBeingDuplicated(EventPlayer()))
    if(Dupping)
    {
        Log(<
            "[ECHO_DUPLICATE_END] [<0>] [<1>], [<2>] [<3>] duplicate ended [<4>] [<5>] [<6>] [<7>] [<8>]",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer()),
            PlayerState.ActualHeroBeingDuplicated(EventPlayer()),
            PlayerState.ActualAlternateForm(EventPlayer()),
            Attacker(),
            HeroOf(Attacker()),
            EventAbility(),
            PlayerState.ActualHeroBeingDuplicated(Attacker()),
            PlayerState.ActualAlternateForm(Attacker())
        >);
        Dupping = false;
    }

//TODO Echo Duplicate Demech
//TODO Echo Duplicate Remech / Bomb

/*
*  @name        TrackD.vaUlts
*  @description Log D.va Ults (remech and bomb)
*/
rule: "TrackD.vaUlts" Event.OngoingPlayer Team.All Player.Dva 
    if(IsGameInProgress())
    if(IsUsingUltimate(EventPlayer()))
    {
        if(Not(CurrentPlayerState.GetState(PlayerStateEnum.IsInAlternateForm)))
        {
            Log(<
                "[DVA_BOMBED] [<0>] [<1>], [<2>]",
                EventPlayer(),
                HeroOf(EventPlayer()),
                TeamOf(EventPlayer())
            >);
        } else {
            Log(<
                "[DVA_REMECH] [<0>] [<1>], [<2>]",
                EventPlayer(),
                HeroOf(EventPlayer()),
                TeamOf(EventPlayer())
            >);
        }
    }

/*
*  @name        TrackKillNormal
*  @description Log Kills that aren't environmental or suicides
*/
rule: "TrackKillNormal" Event.OnFinalBlow
    if(IsGameInProgress())
    if(Not(EventWasEnvironment()))
    {
        Log(<
            "[PLAYER_KILL] [<0>] [<1>], [<2>] killed [<3>] [<4>] using [<5>]",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer()),
            Victim(),
            HeroOf(Victim()),
            EventAbility()
        >);
    }

/*
*  @name        TrackKillEnv
*  @description Log Environmental kills
*/
rule: "TrackKillEnv" Event.OnDeath
    if(IsGameInProgress())
    if(EventPlayer() != Attacker())
    if(EventWasEnvironment())
    {
        Log(<
            "[ENVIRONMENTAL_KILL] [<0>] [<1>], [<2>] killed [<3>] [<4>] using [<5>]",
            Attacker(),
            HeroOf(Attacker()),
            TeamOf(Attacker()),
            EventPlayer(),
            HeroOf(EventPlayer()),
            EventAbility()
        >);
    }

/*
*  @name        TrackSuicideEnv
*  @description Log suicide environmental kills
*/
rule: "TrackSuicideEnv" Event.OnDeath
    if(IsGameInProgress())
    if(EventPlayer() == Attacker())
    if(EventWasEnvironment())
    {
        Log(<
            "[SUICIDE_ENVIRONMENTAL_DEATH] [<0>] [<1>], [<2>] fell from a high place",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer())
        >);
    }

/*
*  @name        TrackSuicideNonEnv
*  @description Log non environmental suicides
*/
rule: "TrackSuicideNonEnv" Event.OnDeath
    if(IsGameInProgress())
    if(Not(EventWasEnvironment()))
    if(EventPlayer() ==Attacker())
    {
        Log(<
            "[SUICIDE] [<0>] [<1>], [<2>] hurt itself in its confusion with [<3>]",
            EventPlayer(),
            HeroOf(EventPlayer()),
            TeamOf(EventPlayer()),
            EventAbility()
        >);
    }

/*
*  @name        TrackHeroSwitch
*  @description Log hero switch during game
*/
rule: "TrackHeroSwitch" Event.OngoingPlayer
    if(HeroOf(EventPlayer()) != TrackedHero)
    {
        if(IsGameInProgress())
        {
            Log(<
                "[HERO_SWITCH] [<0>] from [<1>] swapped to [[<2>] from [<3>]]",
                EventPlayer(),
                TeamOf(EventPlayer()),
                HeroOf(EventPlayer()),
                TrackedHero
            >);
        }
        TrackedHero = HeroOf(EventPlayer());
        Dupping = false;
    }

/*
*  @name        TrackD.vaDemech
*  @description Log D.va Demech (Not as echo dup)
*/
rule: "TrackD.vaDemech" Event.OnDamageTaken Team.All Player.Dva 
    if(IsGameInProgress())
    if(IsAlive())
    if(HasSpawned())
    if(Not(IsInSpawnRoom()))
    if(Not(IsInAlternateForm()))
    if(Not(CurrentPlayerState.GetState(PlayerStateEnum.InSpawn)))
    if(Health() < 0.5)
    {
        Log(<
            "[DEMECH] Player [<0>] [<1>] was demeched by [<2>] [<3>] [<4>]",
            TeamOf(EventPlayer()),
            EventPlayer(),
            TeamOf(Attacker()),
            Attacker(),
            EventAbility()
        >);
    }

/*
*  @name        TrackPrimaryFireUsed
*  @description Log Primary Fire Event
*  @TODO Maybe loop with ammo to make sure to get all event on full auto heroes
*/
rule: "TrackPrimaryFireUsed" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) != Hero.Hanzo)
    if(CurrentPlayerState.IsScoped() == false)
    if(IsFiringPrimary(EventPlayer()))
    {
        Log(AbilityString("PRIMARY_FIRE", EventPlayer()));
    }

/*
*  @name        TrackPrimaryFireUsedHanzo
*  @description Log Primary Fire Event for Hanzo
*  @TODO Maybe loop with ammo to make sure to get all event on full auto heroes
*/
rule: "TrackPrimaryFireUsedHanzo" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) == Hero.Hanzo)
    if(!IsUsingAbility1(EventPlayer()))
    if(IsFiringPrimary(EventPlayer()))
    {
        Log(AbilityString("PRIMARY_FIRE", EventPlayer()));
    }

/*
*  @name        TrackSecondaryFireUsed
*  @description Log Secondary Fire Event
*/
rule: "TrackSecondaryFireUsed" Event.OngoingPlayer
    if(IsGameInProgress())
    if(IsFiringSecondary(EventPlayer()))
    if(HeroOf(EventPlayer()) != Hero.Ana && HeroOf(EventPlayer()) != Hero.Ashe && HeroOf(EventPlayer()) != Hero.Widowmaker && HeroOf(EventPlayer()) != Hero.Sombra)
    {
        Log(AbilityString("SECONDARY_FIRE", EventPlayer()));
    }

/*
*  @name        TrackSecondaryFireUsedScoped
*  @description Log Secondary Fire Event
*/
rule: "TrackSecondaryFireUsedScoped" Event.OngoingPlayer
    if(IsGameInProgress())
    if(CurrentPlayerState.IsScoped() == true)
    if(IsFiringPrimary(EventPlayer()))
    {
        Log(AbilityString("SECONDARY_FIRE", EventPlayer()));
    }

/*
*  @name        TrackSecondaryFireUsedSombraHack
*  @description Log Sombra's Hack
*/
rule: "TrackSecondaryFireSombraHackUsed" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) == Hero.Sombra)
    if(AbilityCooldown(EventPlayer(), Button.SecondaryFire) > 2)
    {
        Log(AbilityString("SECONDARY_FIRE", EventPlayer()));
    }

/*
*  @name        TrackAbility1Used
*  @description Log Ability 1 Event
*/
rule: "TrackAbility1Used" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) != Hero.Hanzo)
    if(IsUsingAbility1(EventPlayer()))
    {
        Log(AbilityString("ABILITY_1", EventPlayer()));
    }

/*
*  @name        TrackAbility2Used
*  @description Log Ability 2 Event
*/
rule: "TrackAbility2Used" Event.OngoingPlayer
    if(IsGameInProgress())
    if(IsUsingAbility2(EventPlayer()))
    {
        Log(AbilityString("ABILITY_2", EventPlayer()));
    }


/*
*  @name        TrackCrouchAbilityUsed
*  @description Log Crouch Ability Event
*/
rule: "TrackCrouchAbilityUsedWreckingBall" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) == Hero.WreckingBall ||
        HeroBeingDuplicated(EventPlayer()) == Hero.WreckingBall)
    if(AbilityCooldown(EventPlayer(), Button.Crouch) != 0)
    {
        Log(AbilityString("CROUCH_ABILITY", EventPlayer()));
    }

/*
*  @name        TrackMeleeUsed
*  @description Log Melee Event
*/
rule: "TrackMeleeUsed" Event.OngoingPlayer
    if(IsGameInProgress())
    if(IsMeleeing(EventPlayer()))
    {
        Log(AbilityString("MELEE", EventPlayer()));
    }

/*
*  @name        TrackReloadingUsed
*  @description Log Reloading Event
*/
rule: "TrackReloading" Event.OngoingPlayer
    if(IsGameInProgress())
    if(IsReloading(EventPlayer()))
    {
        Log(AbilityString("RELOAD", EventPlayer()));
    }



/*
*  @name        TrackHanzoLungeUsed
*  @description Log Hanzo Lunge Event
*/
rule: "TrackHanzoLungeUsed" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) == Hero.Hanzo)
    if(AbilityCooldown(EventPlayer(), Button.Jump) != 0)
    {
       Log(AbilityString("LUNGE", EventPlayer())); 
    }

/*
*  @name        TrackHanzoSonicUsed
*  @description Log Hanzo Lunge Event
*/
rule: "TrackHanzoSonicUsed" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) == Hero.Hanzo)
    if(AbilityCooldown(EventPlayer(), Button.Ability1) != 0)
    {
       Log(AbilityString("ABILITY1", EventPlayer())); 
    }

/*
*  @name        TrackSombraTranslocated
*  @description Log Sombra Activating Translocater
*/
rule: "TrackSombraTranslocated" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) == Hero.Sombra)
    if(IsUsingAbility2(EventPlayer()))
    if(IsButtonHeld(EventPlayer(), Button.Ability2))
    {
       Log(AbilityString("TRANSLOCATE", EventPlayer())); 
    }

/*
*  @name        TrackSombraDecloaked
*  @description Log Sombra Exiting Cloak
*/
rule: "TrackSombraDecloaked" Event.OngoingPlayer
    if(IsGameInProgress())
    if(HeroOf(EventPlayer()) == Hero.Sombra)
    if(IsButtonHeld(EventPlayer(), Button.Ability1))
    if(IsUsingAbility1(EventPlayer()))
    {
       Log(AbilityString("DECLOAK", EventPlayer())); 
    }
//BAPT JUMP CHARGE
//